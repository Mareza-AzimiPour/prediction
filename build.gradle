plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0-RC1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.hibernate.orm' version '7.1.4.Final'
    id 'org.graalvm.buildtools.native' version '0.11.2'

    id 'com.diffplug.spotless' version '8.0.0'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '6.4.4'

    id 'com.github.jakemarsden.git-hooks' version '0.0.2'
}

group = 'com.poma'
version = '0.0.1-SNAPSHOT'
description = 'Poma Football Prediction Software'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.1.0-M4")
    set('springModulithVersion', "2.0.0-RC1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-micrometer-tracing'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
//	implementation 'org.springframework.boot:spring-boot-starter-opentelemetry'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.modulith:spring-modulith-starter-core'
    implementation 'org.springframework.modulith:spring-modulith-starter-jpa'
    implementation 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok'
//	runtimeOnly 'io.micrometer:micrometer-registry-otlp'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.springframework.modulith:spring-modulith-actuator'
    runtimeOnly 'org.springframework.modulith:spring-modulith-observability'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-micrometer-tracing-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-redis-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-opentelemetry-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.17.0')
        importOrder('java', 'javax', 'org', 'com')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

checkstyle {
    toolVersion = '12.1.1'
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
    sourceSets = [sourceSets.main, sourceSets.test]
}

pmd {
    toolVersion = '7.16.0'
    consoleOutput = true
    ignoreFailures = false
    ruleSets = ['category/java/errorprone.xml']
}

tasks.named('pmdMain') {
    exclude '**/build/**'
    exclude '**/generated/**'
    exclude '**/aot/**'
    exclude '**/aotTestSources/**'
}

tasks.named('pmdTest') {
    enabled = false
}

tasks.named('pmdAotTest') {
    enabled = false
}

tasks.named('pmdAot') {
    enabled = false
}

spotbugs {
    toolVersion = '4.9.4'
    ignoreFailures = false
    showProgress = true
}

tasks.register('prePushChecks') {
    group = 'verification'
    description = 'Run formatting and quality checks before push'

    dependsOn 'spotlessApply', 'spotlessCheck', 'checkstyleMain', 'pmdMain', 'spotbugsMain'

    doLast {
        exec {
            commandLine 'git', 'add', '-A'
        }
    }
}

gitHooks {
    hooks = [
            'pre-commit': 'prePushChecks'
    ]
}